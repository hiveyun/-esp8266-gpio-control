fsm {
   * power-on -(@soundAlarm)-> idle,

    idle [
        button1 -(@soundAlarm, relay1.toggle)-,
        button2 -(@soundAlarm, relay2.toggle)-,
        button1LongPress -(@soundAlarm1, smartconfig.smartconfig)-,
        button2LongPress -(@soundAlarm1, smartconfig.smartconfig)-,
        check -(net.check, button1.check, button2.check, led.loop)-,
    ],
}

relay1 {
    * ready --> off,

    off (@relay1Off) [
        toggle --> on,
        on --> on,
        _ -(@soundError)-,
    ],

    on (@relay1On) [
        toggle --> off,
        off --> off,
        _ -(@soundError)-,
    ]
}

relay2 {
    * ready --> off,

    off (@relay2Off) [
        toggle --> on,
        on --> on,
        _ -(@soundError)-,
    ],

    on (@relay2On) [
        toggle --> off,
        off --> off,
        _ -(@soundError)-,
    ]
}

net {
    * ready -(@initWiFi)-> offline,
    offline (@led200) [
        check -(@netCheck, smartconfig.check)-,
        online --> online,
        _ --,
    ],
    online (@led500) [
        check -(@netCheck)-,
        offline --> offline,
        online -(mqtt.check)-,
        _ --,
    ],
}

mqtt {
    * ready -(@initMqtt)-> idle,
    idle [
        check -(@checkPassword)-,
        valid --> connecting,
        invalid --> fetchToken,
        _ --,
    ],
    fetchToken (@startUdpServer) [
        check -(@fetchToken)-,
        done --> connecting,
        _ --,
    ] (@stopUdpServer),
    connecting [
        check -(@connectCheck)-,
        connected --> connected,
        unconnected -(@tryConnect)-,
        failed --> fetchToken,
        _ --,
    ],
    connected (@onConnected, @led1000) [
        check -(@connectCheck)-,
        unconnected --> connecting,
        _ --,
    ] (@led500),
}


button1 {
    * ready --> released,
    released [
        check -(@button1Check)-,
        pressed --> pressed,
        _ --,
    ],
    pressed (@button1EnterPressed) [
        check -(@button1Check)-,
        released -(fsm.button1)-> released,
        longpressed -(fsm.button1LongPress)-> longpressed,
        pressed -(@button1Pressed)-,
        _ --,
    ],
    longpressed [
        check -(@button1Check)-,
        released --> released,
        _ --,
    ],
}

button2 {
    * ready --> released,
    released [
        check -(@button2Check)-,
        pressed --> pressed,
        _ --,
    ],
    pressed (@button2EnterPressed) [
        check -(@button2Check)-,
        released -(fsm.button2)-> released,
        longpressed -(fsm.button2LongPress)-> longpressed,
        pressed -(@button2Pressed)-,
        _ --,
    ],
    longpressed [
        check -(@button2Check)-,
        released --> released,
        _ --,
    ],
}

led {
    * ready -(@initLed)-> off,

    off (@ledOff) [
        loop -(@ledLoop)-,
        toggle --> on,
        _ --,
    ],

    on (@ledOn) [
        loop -(@ledLoop)-,
        toggle --> off,
        _ --,
    ]
}

smartconfig {
    * ready --> idle,
    idle [
        smartconfig --> smartconfig,
        _ --,
    ],
    smartconfig (@led200, @beginSmartconfig, @setMaybeNeedBind) [
        check -(@smartconfigDone)-,
        done -(@soundAlarm)-> idle,
        timeout -(@soundError)-> idle,
        _ --,
    ] (@led1000)
}
