relay {
   * power-on -(@soundAlarm)-> main,

    main [
        button1 -(@soundAlarm, relay1.toggle)-,
        button2 -(@soundAlarm, relay2.toggle)-,
        button2LongPress -(@soundAlarm1, smartconfig.smartconfig)-,
        check -(net.check, button1.check, button2.check)-,
    ],
}

relay1 {
    * ready --> off,

    off (@relay1Off) [
        toggle --> on,
        on --> on,
        _ -(@soundError)-,
    ],

    on (@relay1On) [
        toggle --> off,
        off --> off,
        _ -(@soundError)-,
    ]
}

relay2 {
    * ready --> off,

    off (@relay2Off) [
        toggle --> on,
        on --> on,
        _ -(@soundError)-,
    ],

    on (@relay2On) [
        toggle --> off,
        off --> off,
        _ -(@soundError)-,
    ]
}

net {
    * ready -(@initWiFi)-> offline,
    offline (@led200) [
        check -(@netCheck, smartconfig.check)-,
        online --> online,
        _ --,
    ],
    online (@led500) [
        check -(@netCheck)-,
        offline --> offline,
        online -(mqtt.check)-,
        _ --,
    ],
}

mqtt {
    * ready -(@initMqtt)-> connect,
    connect [
        check -(@connectCheck)-,
        connected --> connected,
        unconnected -(@tryConnect)-> connecting,
        _ --,
    ],
    connecting (@startUdpServer) [
        check -(@connectCheck, @fetchToken)-,
        connected --> connected,
        retry -(@tryConnect)-,
        _ --,
    ] (@stopUdpServer),
    connected (@onConnected, @led1000) [
        check -(@connectCheck)-,
        unconnected --> connect,
        _ --,
    ] (@led500),
}

button1 {
    * ready --> released,
    released [
        check -(@button1Check)-,
        pressed --> pressed,
        _ --,
    ],
    pressed [
        check -(@button1Check)-,
        released -(relay.button1)-> released,
        _ --,
    ],
}

button2 {
    * ready --> released,
    released [
        check -(@button2Check)-,
        pressed --> pressed,
        _ --,
    ],
    pressed (@button2EnterPressed) [
        check -(@button2Check)-,
        released -(relay.button2)-> released,
        longpressed -(relay.button2LongPress)-> longpressed,
        pressed -(@button2Pressed)-,
        _ --,
    ],
    longpressed [
        check -(@button2Check)-,
        released --> released,
        _ --,
    ],
}

led {
    * ready --> off,

    off (@ledOff) [
        toggle --> on,
        _ --,
    ],

    on (@ledOn) [
        toggle --> off,
        _ --,
    ]
}

smartconfig {
    * ready --> idle,
    idle [
        smartconfig --> smartconfig,
        _ --,
    ],
    smartconfig (@led200, @beginSmartconfig) [
        check -(@smartconfigDone)-,
        done -(@soundAlarm)-> idle,
    ] (@led1000)
}
